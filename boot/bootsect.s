!
! SYS_SIZE is the number of clicks (16 bytes) to be loaded.
! 0x3000 is 0x30000 bytes = 196kB, more than enough for current
! versions of linux
!
! ç³»ç»Ÿè¦åŠ è½½çš„æ•°æ®é‡ï¼š196kBã€‚
SYSSIZE = 0x3000
!
!	bootsect.s		(C) 1991 Linus Torvalds
!
! bootsect.s is loaded at 0x7c00 by the bios-startup routines, and moves
! iself out of the way to address 0x90000, and jumps there.
! bootsect.sé€šè¿‡BIOSå¯åŠ¨ç¨‹åºä¸­åŠ è½½åˆ°0x7c00ä½ç½®ï¼Œå¹¶å°†è‡ªèº«ç§»åŠ¨åˆ°0x90000ï¼Œä¸”ç¨‹åºæ‰§è¡Œè·³è½¬åˆ°é‚£é‡Œã€‚
!
! It then loads 'setup' directly after itself (0x90200), and the system
! at 0x10000, using BIOS interrupts.
! ç„¶åä½¿ç”¨BIOSä¸­æ–­åŠ è½½'setup'ï¼ˆ0x90200ï¼‰å’Œç³»ç»Ÿï¼ˆ0x10000ï¼‰ã€‚
!
! NOTE! currently system is at most 8*65536 bytes long. This should be no
! problem, even in the future. I want to keep it simple. This 512 kB
! kernel size should be enough, especially as this doesn't contain the
! buffer cache as in minix
! ç›®å‰ç³»ç»Ÿæœ€é•¿ä¸º512kBã€‚
!
! The loader has been made as simple as possible, and continuos
! read errors will result in a unbreakable loop. Reboot by hand. It
! loads pretty fast by getting whole sectors at a time whenever possible.
! åŠ è½½ç¨‹åºå·²ç»å°½å¯èƒ½ç®€åŒ–ï¼Œå¹¶ä¸”æŒç»­çš„è¯»å–é”™è¯¯å°†å¯¼è‡´ä¸€ä¸ªæ— æ³•ä¸­æ–­çš„å¾ªç¯ã€‚
! è¯·æ‰‹åŠ¨é‡æ–°å¯åŠ¨ã€‚å®ƒé€šè¿‡å°½å¯èƒ½ä¸€æ¬¡è·å–æ•´ä¸ªæ‰‡åŒºæ¥å®ç°å¿«é€ŸåŠ è½½ã€‚

! å£°æ˜å…¨å±€å¯è§æ€§çš„ç¬¦å·ï¼šbegtextã€begdataã€begbssã€endtextã€enddataå’Œendbss
.globl begtext, begdata, begbss, endtext, enddata, endbss
! ç”¨äºæŒ‡å®šä¸‹é¢çš„æŒ‡ä»¤æ˜¯ä»£ç æ®µï¼ˆtext segmentï¼‰çš„ä¸€éƒ¨åˆ†ã€‚ä»£ç æ®µé€šå¸¸ç”¨äºå­˜æ”¾å¯æ‰§è¡ŒæŒ‡ä»¤ã€‚
.text
begtext:
! ç”¨äºæ ‡è®°æ•°æ®æ®µçš„å¼€å§‹ä½ç½®ã€‚
.data
begdata:
! ç”¨äºæŒ‡å®šä¸‹é¢çš„æŒ‡ä»¤æ˜¯BSSæ®µï¼ˆBlock Started by Symbolï¼‰çš„ä¸€éƒ¨åˆ†ã€‚BSSæ®µé€šå¸¸ç”¨äºå­˜æ”¾æœªåˆå§‹åŒ–çš„å…¨å±€å˜é‡ã€‚
.bss
begbss:
.text

SETUPLEN = 4					! nr of setup-sectors setupæ‰‡åŒºçš„æ•°é‡ä¸º4ä¸ªã€‚
BOOTSEG  = 0x07c0			! original address of boot-sector å¼•å¯¼æ‰‡åŒºçš„åŸå§‹åœ°å€ä¸º0x07c0ã€‚
INITSEG  = 0x9000			! we move boot here - out of the way
SETUPSEG = 0x9020			! setup starts here
SYSSEG   = 0x1000			! system loaded at 0x10000 (65536). è¡¨ç¤ºç³»ç»ŸåŠ è½½åˆ°çš„åœ°å€ä¸º0x10000ï¼ˆ65536ï¼‰ã€‚
ENDSEG   = SYSSEG + SYSSIZE		! where to stop loading è¡¨ç¤ºåŠ è½½è¿‡ç¨‹åº”åœæ­¢çš„åœ°å€ã€‚

! ROOT_DEV:	0x000 - same type of floppy as boot.
!		0x301 - first partition on first drive etc
! ROOT_DEVç”¨äºè¡¨ç¤ºæ ¹æ–‡ä»¶ç³»ç»Ÿæ‰€åœ¨çš„è®¾å¤‡:
!   0x000: è¡¨ç¤ºæ ¹æ–‡ä»¶ç³»ç»Ÿä¸å¼•å¯¼æ—¶ä½¿ç”¨çš„è½¯ç›˜ç±»å‹ç›¸åŒã€‚
!   0x301: è¡¨ç¤ºæ ¹æ–‡ä»¶ç³»ç»Ÿä½äºç¬¬ä¸€å—é©±åŠ¨å™¨çš„ç¬¬ä¸€ä¸ªåˆ†åŒºã€‚
!   0x306ï¼šè¡¨ç¤ºæ ¹æ–‡ä»¶ç³»ç»Ÿä½äºç¬¬ä¸€å—é©±åŠ¨å™¨çš„ç¬¬ä¸‰ä¸ªåˆ†åŒºã€‚
ROOT_DEV = 0x306

entry start
start:
	! step 1: å›ºä»¶ç¨‹åº BIOSï¼Œå°†ç¡¬ç›˜ä¸­å¯åŠ¨åŒºçš„ 512 å­—èŠ‚ï¼Œå¤åˆ¶åˆ°å†…å­˜ä¸­çš„0x7c00ä½ç½®ã€‚
	mov	ax,#BOOTSEG
	mov	ds,ax
	mov	ax,#INITSEG
	mov	es,ax
	mov	cx,#256
	sub	si,si
	sub	di,di
	! step 2: å°†å¯åŠ¨åŒº512å­—èŠ‚ä»0x7c00ä½ç½®å¤åˆ¶åˆ°0x9000ä½ç½®ã€‚
	rep
	movw
	jmpi    go,INITSEG
go:	mov	ax,cs
	! step 3: åˆæ­¥åšäº†ä¸€æ¬¡å†…å­˜è§„åˆ’ã€‚ä»CPUçš„è§’åº¦çœ‹ï¼Œè®¿é—®ä¸‰ä¸ªåœ°æ–¹çš„å†…å­˜ï¼Œä¸ºæ•°æ®æ®µå’Œä»£ç æ®µï¼š0x9000ï¼Œæ ˆé¡¶ï¼š0x9FF00ã€‚
	mov	ds,ax
	mov	es,ax
! put stack at 0x9ff00.
	mov	ss,ax
	mov	sp,#0xFF00		! arbitrary value >>512

! load the setup-sectors directly after the bootblock.
! Note that 'es' is already set up.

! step 4: ç¡¬ç›˜çš„ç¬¬2ä¸ªæ‰‡åŒºå¼€å§‹ï¼ŒæŠŠæ•°æ®åŠ è½½åˆ°å†…å­˜0x90200å¤„ï¼Œå…±åŠ è½½4ä¸ªæ‰‡åŒº
load_setup:
	mov	dx,#0x0000		! drive 0, head 0
	mov	cx,#0x0002		! sector 2, track 0
	mov	bx,#0x0200		! address = 512, in INITSEG
	mov	ax,#0x0200+SETUPLEN	! service 2, nr of sectors
	! bios 13å·ä¸­æ–­ã€‚å¯¹åº”çš„åŠŸèƒ½æœ‰å¾ˆå¤šï¼Œç”±axä¼ å…¥ä½¿ç”¨å“ªä¸ªåŠŸèƒ½ã€‚è¿™é‡Œä½¿ç”¨çš„æ˜¯åŠŸèƒ½2ï¼Œè¯»å–æ‰‡åŒºæ•°æ®ã€‚
	! AHï¼åŠŸèƒ½å·ï¼›ALï¼æ‰‡åŒºæ•°ï¼›CHï¼æŸ±é¢ï¼›CLï¼æ‰‡åŒºï¼›DHï¼ç£å¤´ï¼›
	! DLï¼é©±åŠ¨å™¨ï¼Œ00H~7FHï¼šè½¯ç›˜ï¼›80H~0FFHï¼šç¡¬ç›˜ï¼›
	! ES:BXï¼ç¼“å†²åŒºçš„åœ°å€ï¼›
	! è¿”å›ï¼šCFï¼0è¯´æ˜æ“ä½œæˆåŠŸï¼Œå¦åˆ™ï¼ŒAHï¼é”™è¯¯ä»£ç 
    !
	! è¯»å–è½¯ç›˜çš„setupæ¨¡å—ä»£ç ï¼Œjcåœ¨CF=1æ—¶è·³è½¬ï¼Œjncåˆ™åœ¨CF=0æ—¶è·³è½¬ï¼Œ
	! è¯»å–è½¯ç›˜å‡ºé”™åˆ™CF=1ï¼Œahæ˜¯å‡ºé”™ç ï¼Œæ‰€ä»¥ä¸‹é¢æ˜¯CFç­‰äº1ï¼Œè¯´æ˜åŠ è½½æˆåŠŸï¼Œåˆ™è·³è½¬ï¼Œå¦åˆ™åˆ™é‡è¯•ã€‚
	int	0x13			! read it
	jnc	ok_load_setup		! ok - continue
	mov	dx,#0x0000
	mov	ax,#0x0000		! reset the diskette
	int	0x13
	j	load_setup

ok_load_setup:

! Get disk drive parameters, specifically nr of sectors/track

	mov	dl,#0x00
	mov	ax,#0x0800		! AH=8 is get drive parameters
	int	0x13
	mov	ch,#0x00
	seg cs
	mov	sectors,cx
	mov	ax,#INITSEG
	mov	es,ax

! Print some inane message

	mov	ah,#0x03		! read cursor pos
	xor	bh,bh
	int	0x10
	
	mov	cx,#24
	mov	bx,#0x0007		! page 0, attribute 7 (normal)
	mov	bp,#msg1
	mov	ax,#0x1301		! write string, move cursor
	int	0x10

! ok, we've written the message, now
! we want to load the system (at 0x10000)

	! step 5: åŠ è½½systemæ¨¡å—ä»£ç 
	! æŠŠä»ç¡¬ç›˜ç¬¬ 6 ä¸ªæ‰‡åŒºå¼€å§‹å¾€åçš„ 240 ä¸ªæ‰‡åŒºï¼ŒåŠ è½½åˆ°å†…å­˜ 0x10000 å¤„
	mov	ax,#SYSSEG
	mov	es,ax		! segment of 0x010000
	call	read_it
	call	kill_motor

! After that we check which root-device to use. If the device is
! defined (!= 0), nothing is done and the given device is used.
! Otherwise, either /dev/PS0 (2,28) or /dev/at0 (2,8), depending
! on the number of sectors that the BIOS reports currently.

	seg cs
	mov	ax,root_dev
	cmp	ax,#0
	jne	root_defined
	seg cs
	mov	bx,sectors
	mov	ax,#0x0208		! /dev/ps0 - 1.2Mb
	cmp	bx,#15
	je	root_defined
	mov	ax,#0x021c		! /dev/PS0 - 1.44Mb
	cmp	bx,#18
	je	root_defined
undef_root:
	jmp undef_root
root_defined:
	seg cs
	mov	root_dev,ax

! after that (everyting loaded), we jump to
! the setup-routine loaded directly after
! the bootblock:
  ! step 6: ğŸ€ åŠ è½½å®Œsetupå’Œsystemæ¨¡å—ï¼Œè·³åˆ°setupæ¨¡å—æ‰§è¡Œ
	jmpi	0,SETUPSEG

! This routine loads the system at address 0x10000, making sure
! no 64kB boundaries are crossed. We try to load it as fast as
! possible, loading whole tracks whenever we can.
!
! in:	es - starting address segment (normally 0x1000)
!
sread:	.word 1+SETUPLEN	! sectors read of current track
head:	.word 0			! current head
track:	.word 0			! current track

read_it:
	mov ax,es
	test ax,#0x0fff
die:	jne die			! es must be at 64kB boundary
	xor bx,bx		! bx is starting address within segment
rp_read:
	mov ax,es
	cmp ax,#ENDSEG		! have we loaded all yet?
	jb ok1_read
	ret
ok1_read:
	seg cs
	mov ax,sectors
	sub ax,sread
	mov cx,ax
	shl cx,#9
	add cx,bx
	jnc ok2_read
	je ok2_read
	xor ax,ax
	sub ax,bx
	shr ax,#9
ok2_read:
	call read_track
	mov cx,ax
	add ax,sread
	seg cs
	cmp ax,sectors
	jne ok3_read
	mov ax,#1
	sub ax,head
	jne ok4_read
	inc track
ok4_read:
	mov head,ax
	xor ax,ax
ok3_read:
	mov sread,ax
	shl cx,#9
	add bx,cx
	jnc rp_read
	mov ax,es
	add ax,#0x1000
	mov es,ax
	xor bx,bx
	jmp rp_read

read_track:
	push ax
	push bx
	push cx
	push dx
	mov dx,track
	mov cx,sread
	inc cx
	mov ch,dl
	mov dx,head
	mov dh,dl
	mov dl,#0
	and dx,#0x0100
	mov ah,#2
	int 0x13
	jc bad_rt
	pop dx
	pop cx
	pop bx
	pop ax
	ret
bad_rt:	mov ax,#0
	mov dx,#0
	int 0x13
	pop dx
	pop cx
	pop bx
	pop ax
	jmp read_track

/*
 * This procedure turns off the floppy drive motor, so
 * that we enter the kernel in a known state, and
 * don't have to worry about it later.
 */
kill_motor:
	push dx
	mov dx,#0x3f2
	mov al,#0
	outb
	pop dx
	ret

sectors:
	.word 0

msg1:
	.byte 13,10
	.ascii "Loading system ..."
	.byte 13,10,13,10

.org 508
root_dev:
	.word ROOT_DEV
boot_flag:
	.word 0xAA55

.text
endtext:
.data
enddata:
.bss
endbss:
